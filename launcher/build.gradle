apply plugin: 'com.android.application'
//apply plugin: 'android-apt'
//apply plugin: 'com.ppfuns.toolkit'

//toolkit{
//    projectName 'launcher'
//}

//获取当前编译时间
def getCreateTime() {
    def createTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00")).trim()
    return createTime
}

/**
 * 获取Git 版本号
 */
def getGitSHA() {
    def gitSHA = 'git rev-parse --short HEAD'.execute().text.trim()
    return gitSHA
}

/**
 * 获取Git 提交次数
 */
def getGitCommitCount() {
    def gitCommitCount = 'git rev-list --count HEAD'.execute().text.trim();
    return gitCommitCount;
}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount.toInteger()
        versionName "v1.0." + gitSHA
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        lintOptions {
            abortOnError false
        }
    }

    signingConfigs {
        release {
            keyAlias 'android.keystore'
            keyPassword 'ppfuns123'
            storeFile file('./sign/android.keystore')
            storePassword 'ppfuns123'
        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('./sign/platform.keystore')
            storePassword 'android'
        }
    }

    buildTypes {
        release {
//            minifyEnabled true //混淆
//            zipAlignEnabled true//Zipalign优化
//            shrinkResources true// 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    //配置自定义打包名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName
            if (variant.buildType.name.equals('release')) {
                fileName = "${applicationId}_release_${gitCommitCount}_${createTime}_${versionName}.apk"
            } else if (variant.buildType.name.equals('debug')) {
                fileName = "${applicationId}_debug_${gitCommitCount}_${createTime}_${versionName}.apk"
            }
            outputFileName = fileName;
        }
    }

    sourceSets {
        hengyang {
            res.srcDirs = ['src/hengyang/res']
            java.srcDirs = ['src/hengyang/java']
        }

        alliance {
            res.srcDirs = ['src/alliance/res']
            java.srcDirs = ['src/alliance/java']
        }
    }

    productFlavors {//分支信息，程序里添加分支项时名称及包名必须于该处定义一致
        hengyang {
            applicationId "com.ppfuns.launcher"
            minSdkVersion rootProject.ext.minSdkVersion
            targetSdkVersion rootProject.ext.targetSdkVersion
            versionCode gitCommitCount.toInteger()
            versionName "v1.0." + gitSHA
        }
        alliance {
            //com.alliance.homeshell
            applicationId "com.alliance.homeshell"
            minSdkVersion rootProject.ext.minSdkVersion
            targetSdkVersion rootProject.ext.targetSdkVersion
            versionCode gitCommitCount.toInteger()
            versionName "v1.0." + gitSHA
        }

    }

}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.android.support:palette-v7:25.3.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.umeng.analytics:analytics:5.6.7'
    implementation project(path: ':ppfunstv')
}
