apply plugin: 'com.android.application'
//apply plugin: 'android-apt'
//apply plugin: 'com.flyzebra.toolkit'

//toolkit{
//    projectName 'launcher'
//}

//获取当前编译时间
def getCreateTime() {
    def createTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00")).trim()
    return createTime
}

/**
 * 获取Git 版本号
 */
def getGitSHA() {
    def gitSHA = 'git rev-parse --short HEAD'.execute().text.trim()
    return gitSHA
}

/**
 * 获取Git 提交次数
 */
def getGitCommitCount() {
    def gitCommitCount = 'git rev-list --count HEAD'.execute().text.trim();
    return gitCommitCount;
}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.flyzebra.launcher2"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount.toInteger()
        versionName "v1.0." + gitSHA
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        lintOptions {
            abortOnError false
        }
    }

    signingConfigs {
        release {
            keyAlias 'android.keystore'
            keyPassword 'ppfuns123'
            storeFile file('./sign/android.keystore')
            storePassword 'ppfuns123'
        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('./sign/platform.keystore')
            storePassword 'android'
        }
    }

    buildTypes {
        release {
//            minifyEnabled true //混淆
//            zipAlignEnabled true//Zipalign优化
//            shrinkResources true// 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    //配置自定义打包名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName
            if (variant.buildType.name.equals('release')) {
                fileName = "${applicationId}_release_${gitCommitCount}_${createTime}_${versionName}.apk"
            } else if (variant.buildType.name.equals('debug')) {
                fileName = "${applicationId}_debug_${gitCommitCount}_${createTime}_${versionName}.apk"
            }
            outputFileName = fileName;
        }
    }

}


dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation project(path: ':flyuitvlib')
    implementation project(path: ':flyuilib')
}
